# run with gnuplot -e "filename='fes.dat'; a = 1; b = 2; c = 3" ~/my_scripts/plot_contour.plg
# or 
# 
#set cntrparam levels incr 0.0,2.0,100.0
#set term x11 size 1920, 1024
set term png size 1024, 1024 enhanced
set encoding iso_8859_1; 
set output filename.".png"

set view map 
set pm3d interpolate 25,25
set multiplot
set lmargin at screen 0.15
set rmargin at screen 0.85
set bmargin at screen 0.15
set tmargin at screen 0.8

#set title "Свободная энергия нековалентного связывания\n R ХОР и A5Bad\n" font ",30" offset 1,4
set size square
set xrange [5:30]
set xlabel "Расстояние Cb-P, \305 " font ",30" offset 0,-2
set xtics font ",20"
set yrange [0:180]
set ylabel "Угол Cb-P-O1"  font ",30" offset -2,0
set ytics font ",20"
set format y "%0.f\260"

set cbrange [0:60]
set cbtics 10 font ",20"
set cblabel "Свободная энергия, кДж/моль"  font ",30" offset 6

unset key
set palette rgbformulae 33,13,10
set palette maxcolors 6
#21,22,23
sp filename u ($1*10):($2*57.3):3 w pm3d 
unset surface 
set contour base
set cntrlabel onecolor

#set cntrparam cubicspline
set cntrparam bspline
set cntrparam order 3
set cntrparam points 5
set cntrparam levels incremental 0, 10, 60

set linetype 1 lc rgb "black" lw 2.5 pt 1
set cntrlabel format '%8.2f' font ',15' start 10 interval -1

x1 = 8 
x2 = 10
y1 = 140
y2 = 180
set arrow from x1,y1 to x2,y1 nohead lc rgb "black" lw 6 
set arrow from x2,y1 to x2,y2 nohead lc rgb "black" lw 6
set arrow from x2,y2 to x1,y2 nohead lc rgb "black" lw 6
set arrow from x1,y2 to x1,y1 nohead lc rgb "black" lw 6

#sp filename u ($3<=65?$1:1/0):($3<=65?$2:1/0):3 w l
sp filename u ($1*10):($2*57.3):3 w l

set cntrparam levels incremental 0, 10, 60
set linetype 1 lc rgb "black" lw 2 pt 1
#sp filename u ($3<=50?$1:1/0):($3<=50?$2:1/0):3 w labels 


unset multiplot
#pause -1

